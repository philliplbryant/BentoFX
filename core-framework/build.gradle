plugins {
    id 'java'
    id 'maven-publish'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.jreleaser' version '1.22.0'
    id 'xyz.wagyourtail.jvmdowngrader' version '1.3.4'
}

dependencies {
    // Provided dependencies
    compileOnly 'org.openjfx:javafx-controls:19.0.2.1'
    compileOnly 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    testCompileOnly 'jakarta.annotation:jakarta.annotation-api:3.0.0'

    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-surefire-provider:1.3.2'
    testImplementation 'org.testfx:testfx-junit5:4.0.18'
}

javafx {
    version = '19'
    modules = ['javafx.controls']
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }

    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-parameters'
}

jvmdg {
    downgradeTo = JavaVersion.VERSION_17
}

javadoc {
    options {
        addStringOption('Xdoclint:none', '-quiet')
    }
}

// This configuration is required because the BoxApp *application* is included
// in the test folder but there are no actual *tests* in the folder.
// FIXME ISSUE-13: Move the BoxApp application to a separate module?
test {
    failOnNoDiscoveredTests = false
}

    // There is currently a bug with the shading portion of 'jvmdowngrader' which
    // prevents the 'module-info' class from being copied into the final output.
    // These two tasks will generate a modified shaded output jar that includes it.
tasks.register('copyModuleInfo', Copy) {
        // Ensure the downgrader tasks all run
        dependsOn(tasks.named("shadeDowngradedApi"))

        // Copy the downgraded module-info into a temporary location
        from(zipTree(tasks.named("downgradeJar").get().outputs.files.singleFile)) {
            include('module-info.class')
        }
        into(project.layout.buildDirectory.dir('module-info-temp'))
    }
tasks.register('mergeShadedJar', Jar) {
        // Run the copy task defined above
        dependsOn(tasks.named('copyModuleInfo'))

        archiveClassifier.set('merged-shaded')

        // Merge the shaded output and our module-info class we put in the temp dir
        from(zipTree(tasks.named("shadeDowngradedApi").get().outputs.files.singleFile))
        from(project.layout.buildDirectory.dir('module-info-temp'))
    }

// Ensure builds emit the corrected shaded jar
assemble.dependsOn mergeShadedJar

// Setup publishing to pull from our slightly modified outputs
def includeTransitives = false
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            // Include our corrected shaded/downgraded jar
            artifact(tasks.named('mergeShadedJar').get().outputs.files.singleFile) {
                builtBy tasks.named('mergeShadedJar')
            }

            // Include sources + javadoc jars
            artifact(tasks.named('sourcesJar').get())
            artifact(tasks.named('javadocJar').get())

            pom {
                name = project.name
                description = 'A docking system for JavaFX.'
                url = 'https://github.com/Col-E/BentoFX'
                inceptionYear = '2025'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
                developers {
                    developer {
                        id = 'Col-E'
                        name = 'Matt Coley'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/Col-E/BentoFX.git'
                    developerConnection = 'scm:git:ssh://github.com/Col-E/BentoFX.git'
                    url = 'https://github.com/Col-E/BentoFX'
                }

                // We aren't using "from components.java" so we need to rebuild the dependencies node ourselves
                if (includeTransitives) withXml {
                    def allDeps = project.configurations.compileClasspath.resolvedConfiguration.firstLevelModuleDependencies
                    def root = asNode()
                    def depNode = root.appendNode("dependencies")
                    allDeps.each { d ->
                        def dn = depNode.appendNode("dependency")
                        dn.appendNode("groupId", d.moduleGroup)
                        dn.appendNode("artifactId", d.name)
                        dn.appendNode("version", d.moduleVersion)
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        pgp {
            active = 'RELEASE'
            armored = true
        }
    }
    release {
        // TODO: This doesn't auto-publish github releases and the 'distribution' block also isn't a viable alternative
        //  Need to look into why it doesn't work. Probably related to the project's "alternative" artifact model...
        github {
            tagName = project.version
            changelog {
                formatted = 'ALWAYS'
                preset = 'conventional-commits'
                contributors {
                    format = '- {{contributorName}}{{#contributorUsernameAsLink}} ({{.}}){{/contributorUsernameAsLink}}'
                }
            }
        }
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    applyMavenCentralRules = true
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}
